abstract type AbstractUncertaintyProblem end

"""
```julia
ExpectationProblem(S, g, h, pdist, params)
ExpectationProblem(g, pdist, params)
ExpectationProblem(sm::SystemMap, g, h, d)
```

Defines âˆ« g(S(h(x,u0,p)))*f(x)dx

## Arguments

Let ğ• = uncertainty space, ğ•Œ = Initial condition space, â„™ = model parameter space

  - S: ğ•Œ Ã— â„™ â†’ ğ•Œ also known as system map.
  - g: ğ•Œ Ã— â„™ â†’ â„â¿áµ’áµ˜áµ— also known as the observables or output function.
  - h: ğ• Ã— ğ•Œ Ã— â„™ â†’ ğ•Œ Ã— â„™ also known as covariate function.
  - pdf(d,x): ğ• â†’ â„ the uncertainty distribution of the initial states.
  - params
"""
struct ExpectationProblem{TS, TG, TH, TF, TP} <: AbstractUncertaintyProblem
    # defines âˆ« g(S(h(x,u0,p)))*f(x)dx
    # ğ• = uncertainty space, ğ•Œ = Initial condition space, â„™ = model parameter space,
    S::TS  # mapping,                 S: ğ•Œ Ã— â„™ â†’ ğ•Œ
    g::TG  # observable(output_func), g: ğ•Œ Ã— â„™ â†’ â„â¿áµ’áµ˜áµ—
    h::TH  # cov(input_func),         h: ğ• Ã— ğ•Œ Ã— â„™ â†’ ğ•Œ Ã— â„™
    d::TF  # distribution,            pdf(d,x): ğ• â†’ â„
    params::TP
end

# Constructor for general maps/functions
function ExpectationProblem(g, pdist, params; nout = nothing)
    !isnothing(nout) && @warn "nout is deprecated and unused"
    h(x, u, p) = x, p
    S(x, p) = x
    ExpectationProblem(S, g, h, pdist, params)
end

# Constructor for DEProblems
function ExpectationProblem(sm::SystemMap, g, h, d; nout = nothing)
    !isnothing(nout) && @warn "nout is deprecated and unused"
    ExpectationProblem(sm, g, h, d,
        ArrayPartition(deepcopy(sm.prob.u0), deepcopy(sm.prob.p)))
end

distribution(prob::ExpectationProblem) = prob.d
mapping(prob::ExpectationProblem) = prob.S
observable(prob::ExpectationProblem) = prob.g
input_cov(prob::ExpectationProblem) = prob.h
parameters(prob::ExpectationProblem) = prob.params

##
# struct CentralMomentProblem
#     ns::NTuple{Int,N}
#     altype::Union{NestedExpectation, BinomialExpansion} #Should rely be in solve
#     exp_prob::ExpectationProblem
# end

function ExpectationProblem(sm::ProcessNoiseSystemMap, g, h; nout = nothing)
    !isnothing(nout) && @warn "nout is deprecated and unused"
    d = GenericDistribution((Truncated(Normal(), -4.0, 4.0) for i in 1:(sm.n))...)
    ExpectationProblem(sm, g, h, d, deepcopy(sm.prob.p))
end
